# uninstall support for CMake

if(NOT EXISTS "@CMAKE_BINARY_DIR@/install_manifest.txt")
    message(FATAL_ERROR "Cannot find install manifest: @CMAKE_BINARY_DIR@/install_manifest.txt.\nRun 'make install' before uninstall")
endif()

message(STATUS "Uninstall configuration \"@CMAKE_BUILD_TYPE@\":")

file(READ "@CMAKE_BINARY_DIR@/install_manifest.txt" files)
string(REGEX REPLACE "\n" ";" files "${files}")
foreach(file ${files})
    message(STATUS "Uninstalling: $ENV{DESTDIR}${file}")
    if(IS_SYMLINK "$ENV{DESTDIR}${file}" OR EXISTS "$ENV{DESTDIR}${file}")
        if(CMAKE_VERSION VERSION_LESS "3.0")
            exec_program("@CMAKE_COMMAND@" ARGS "-E remove \"$ENV{DESTDIR}${file}\""
                OUTPUT_VARIABLE rm_out
                RETURN_VALUE rm_retval
            )
        else()
            execute_process(COMMAND @CMAKE_COMMAND@ -E remove "$ENV{DESTDIR}${file}"
                            OUTPUT_VARIABLE rm_out
                            RESULT_VARIABLE rm_retval
            )
        endif()

        if(NOT "${rm_retval}" STREQUAL 0)
            message(FATAL_ERROR "Problem when removing $ENV{DESTDIR}${file}")
        endif()
    else()
        message(STATUS "  Note: File $ENV{DESTDIR}${file} does not exist.")
    endif()
endforeach()
